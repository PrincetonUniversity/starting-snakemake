rule all:
    input:
        "results/princeton_bar.png",


rule download_princeton:
    '''Download the raw html from a url.'''
    output:
        html="data/princeton.html",
    params:
        url="https://www.princeton.edu"
    shell:
        'curl -o {output.html} {params.url}'


rule tokenize_princeton:
    '''Given an html, remove html tags and produce one word per line.'''
    input:
        html="data/princeton.html",
    output:
        words="data/princeton_words.txt",
    params:
        sed_command=(
                r'/^ *<script/d ; '  # delete all lines with script tags
                r's/<[^>]*>//g ; '  # remove all remaining tags
                r's/[^a-zA-Z]\+/\n/g'  # replace any non-alphabetical characters with newlines
                )
    shell:
        "sed '{params.sed_command}' {input.html} |"
        "grep -v '^$' "  # remove empty lines
        "> {output.words}"


rule count_princeton:
    '''Produce a datatable from the raw list of words.'''
    input:
        words="data/princeton_words.txt",
    output:
        counts="results/princeton_counts.csv",
    run:
        from collections import Counter
        import pandas as pd

        # count the frequency of each word
        word_counts = Counter(
                line.strip().lower()
                for line in open(input['words']))

        # generate dataframe from count dictionary
        df = pd.DataFrame.from_dict(word_counts, orient='index', columns=['count'])
        # move the words from index to a separate column
        df = df.reset_index(names='word')
        # sort with most frequent words on top
        df = df.sort_values('count', ascending=False)
        # save csv
        df.to_csv(output['counts'])


rule plot_princeton:
    '''Generate a bar chart of word frequencies from the tabulated counts.'''
    input:
        counts="results/princeton_counts.csv",
    output:
        chart="results/princeton_bar.png",
    run:
        import pandas as pd
        import matplotlib
        matplotlib.use('Agg')
        import matplotlib.pyplot as plt
        df = pd.read_csv(input['counts'])
        ax = df[df.word.str.len() > 4].iloc[:20].plot.bar(x='word', y='count')
        plt.tight_layout()
        ax.figure.savefig(output['chart'])
